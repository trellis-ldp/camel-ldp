plugins {
    id 'com.github.hierynomus.license' version '0.14.0'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'net.researchgate.release' version '2.7.0'
    id 'com.github.kt3k.coveralls' version '2.8.2'
    id 'org.sonarqube' version '2.6.2'
}

apply plugin: 'maven'
apply plugin: 'osgi'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'checkstyle'
apply plugin: 'com.github.hierynomus.license'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8


group = 'org.trellisldp'
description = 'LDP Utilities for Apache Camel'

ext {
    moduleName = 'org.trellisldp.camel'
    vendor = 'Trellis LDP'
    homepage = 'https://www.trellisldp.org'
    docURL = 'https://trellis-ldp.github.io/trellis/apidocs/'
    license = 'Apache 2'

    /* Dependencies */
    camelVersion = '2.21.1'

    /* Testing */
    activationVersion = '1.1.1'
    apiguardianVersion = '1.0.0'
    checkstyleVersion = '8.10'
    jaxbVersion = '2.3.0'
    jacocoVersion = '0.8.1'
    junitPlatformVersion = '1.2.0'
    junitVersion = '5.2.0'
    logbackVersion = '1.2.3'

    /* OSGi */
    camelVersionRange = '[2.16,3)'
    projectOsgiVersion = project.version.replaceAll("-SNAPSHOT", ".SNAPSHOT")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

dependencies {
    api group: 'org.apache.camel', name: 'camel-core', version: camelVersion

    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
    testImplementation group: 'org.apiguardian', name: 'apiguardian-api', version: apiguardianVersion
    testImplementation group: 'org.junit.platform', name: 'junit-platform-runner', version: junitPlatformVersion
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: junitVersion
    testImplementation group: 'org.apache.camel', name: 'camel-test', version: camelVersion
    testImplementation group: 'javax.xml.bind', name: 'jaxb-api', version: jaxbVersion
    testImplementation group: 'javax.activation', name: 'activation', version: activationVersion
}

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
    }
}

test {
    jacoco {
        enabled = ! JavaVersion.current().isJava11Compatible()
    }
}

release {
    tagTemplate = '$name-$version'
}

jar {
    from ("$rootDir/LICENSE") {
        into "META-INF"
    }
    manifest {
        description project.description
        docURL project.docURL
        vendor project.vendor
        license project.license

        instruction 'Automatic-Module-Name', moduleName
        instruction 'Import-Package', '*'
        instruction 'Export-Package', "${moduleName};version=${projectOsgiVersion}"
    }
}

task sourceJar(type: Jar) {
    classifier 'sources'
    from ("$rootDir/LICENSE") {
        into "META-INF"
    }
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier 'javadoc'
    from ("$rootDir/LICENSE") {
        into "META-INF"
    }
    from javadoc
}

artifacts {
    archives javadocJar
    archives sourceJar
    archives (file('build/resources/main/features.xml')) {
        classifier 'features'
        type 'xml'
    }
}

jacoco {
    toolVersion = jacocoVersion
}

license {
    include "**/*.java"
    header rootProject.file('buildtools/src/main/resources/license/HEADER.txt')
    strictCheck true
    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

processResources {
    outputs.upToDateWhen { false }
    expand project.properties
}

signing {
    required { !version.endsWith("SNAPSHOT") && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

uploadArchives {
    repositories.mavenDeployer {
        def sonatypeUsername = project.hasProperty('ossrhUsername') ? ossrhUsername : ""
        def sonatypePassword = project.hasProperty('ossrhPassword') ? ossrhPassword : ""
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
            authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
            packaging 'jar'
            url 'http://www.trellisldp.org'
            inceptionYear '2017'
            name 'LDP Utilities for Apache Camel'
            description 'Utilities for asynchronous integrations with Apache Camel'

            organization {
                name project.vendor
                url project.homepage
            }

            developers {
                developer {
                    id 'acoburn'
                    name 'Aaron Coburn'
                    email 'acoburn @ (domain of organization url)'
                    organization 'Amherst College'
                    organizationUrl 'https://www.amherst.edu'
                    roles {
                        role 'developer'
                    }
                    timezone '-5'
                }
            }

            scm {
                connection 'scm:git:git://github.com/trellis-ldp/camel-ldp.git'
                developerConnection 'scm:git:git@github.com/trellis-ldp/camel-ldp.git'
                url 'https://github.com/trellis-ldp/camel-ldp'
                tag 'HEAD'
            }

            licenses {
                license {
                    name 'Apache License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0'
                    comments 'Copyright (c) 2017-2018 Trellis LDP'
                }
            }
        }
    }
}

checkstyle {
    configFile = rootProject.file('buildtools/src/main/resources/checkstyle/checkstyle.xml')
    configProperties.checkstyleConfigDir = rootProject.file('buildtools/src/main/resources/checkstyle/')
    toolVersion = checkstyleVersion
}

// Ignore alpha, beta, milestone and release candidates
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject("Release Candidate")
            }
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectName", "LDP Utilities for Camel"
        property "sonar.projectKey", "org.trellisldp:camel-ldp"
        property "sonar.links.homepage", "https://github.com/trellis-ldp/trellis"
        property "sonar.links.issue", "https://github.com/trellis-ldp/camel-ldp/issues"
        property "sonar.links.scm_dev", "scm:git:git@github.com:trellis-ldp/camel-ldp.git"
    }
}

task docs(type: Javadoc) {
    outputs.upToDateWhen { false }
    source sourceSets.main.allJava
    classpath = files(sourceSets.main.compileClasspath)
    destinationDir = new File(projectDir, "docs/${version}")
    options {
        links "http://docs.oracle.com/javase/8/docs/api/"
    }
}

afterReleaseBuild.dependsOn uploadArchives
afterReleaseBuild.dependsOn docs

